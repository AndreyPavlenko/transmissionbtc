def version = '1.3.5'
def versionNum = 9000001

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'

def localProps = new Properties()
localProps.load(file('local.properties').newDataInputStream())

android {
    dataBinding.enabled true
    externalNativeBuild.cmake.path 'CMakeLists.txt'

    defaultConfig {
        applicationId 'com.ap.transmission.btc'
        minSdkVersion 21
        targetSdkVersion 29
        compileSdkVersion 29
        versionCode versionNum
        versionName version

        externalNativeBuild {
            cmake {
                targets 'transmissionbtc'
                arguments "-DDEPENDS_ROOT=${localProps['depends']}"
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(localProps["kestorePath"])
            storePassword localProps["kestorePassword"]
            keyAlias localProps["kestoreAlias"]
            keyPassword localProps["kestoreKeyPassword"]
        }
    }

    sourceSets {
        main {
            assets.srcDirs += "${localProps['depends']}/arm64-api21/usr/share/transmission"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task cleanAll(type: Delete) {
    delete project.buildDir
}
